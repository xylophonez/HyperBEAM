%%% @doc A device that triggers repass events until a certain counter has been
%%% reached. This is useful for certain types of stacks that need various
%%% execution passes to be completed in sequence across devices.
-module(dev_multipass).
-export([info/1]).
-include("include/hb.hrl").
-include_lib("eunit/include/eunit.hrl").

info(_M1) ->
    #{
        handler => fun handle/4
    }.

%% @doc Forward the keys function to the message device, handle all others
%% with deduplication. We only act on the first pass.
-spec handle(binary(), #{ passes => integer(), pass => integer(), _ => _ }, #{ _ => _ }, #{ _ => _ }) ->
    {ok, #{ _ => _ }} | {pass, #{ _ => _ }}.
handle(<<"keys">>, M1, _M2, Opts) ->
    hb_ao:raw(<<"message@1.0">>, <<"keys">>, M1, #{}, Opts);
handle(<<"set">>, M1, M2, Opts) ->
    hb_ao:raw(<<"message@1.0">>, <<"set">>, M1, M2, Opts);
handle(_Key, M1, _M2, Opts) ->
    Passes = hb_ao:get(<<"passes">>, {as, <<"message@1.0">>, M1}, 1, Opts),
    Pass = hb_ao:get(<<"pass">>, {as, <<"message@1.0">>, M1}, 1, Opts),
    case Pass < Passes of
        true -> {pass, M1};
        false -> {ok, M1}
    end.

%%% Tests

basic_multipass_test() ->
    Base =
        #{
            <<"device">> => <<"multipass@1.0">>,
            <<"passes">> => 2,
            <<"pass">> => 1
        },
    Req = Base#{ <<"pass">> => 2 },
    ?assertMatch({pass, _}, hb_ao:resolve(Base, <<"Compute">>, #{})),
    ?event(alive),
    ?assertMatch({ok, _}, hb_ao:resolve(Req, <<"Compute">>, #{})).
