%%% @doc Test vectors for the `~process@1.0' and associated subsystems.
-module(dev_process_test_vectors).
-include("include/hb.hrl").
-include_lib("eunit/include/eunit.hrl").
%%% Helpers used by other devices that utilize `~process@1.0'.
-export([init/0, aos_process/0, aos_process/1, test_process/0, wasm_process/1]).
-export([schedule_aos_call/2, schedule_aos_call/3]).

init() -> application:ensure_all_started(hb).

test_opts() ->
    test_opts(#{}).
test_opts(Opts) ->
    init(),
    Opts#{
        <<"store">> => hb_test_utils:test_store(hb_store_lmdb),
        <<"priv-wallet">> => ar_wallet:new()
    }.

%% @doc Generate a process message with a random number, and no executor.
base_process(Opts) ->
    Wallet = hb_opts:get(priv_wallet, hb:wallet(), Opts),
    Address = hb_util:human_id(ar_wallet:to_address(Wallet)),
    hb_message:commit(
        #{
            <<"device">> => <<"process@1.0">>,
            <<"scheduler-device">> => <<"scheduler@1.0">>,
            <<"scheduler-location">> => hb_opts:get(scheduler, Address, Opts),
            <<"type">> => <<"Process">>,
            <<"test-random-seed">> => rand:uniform(1337)
        },
        Opts#{ <<"priv-wallet">> => Wallet }
    ).

wasm_process(WASMImage) ->
    wasm_process(WASMImage, #{}).
wasm_process(WASMImage, Opts) ->
    Wallet = hb_opts:get(priv_wallet, hb:wallet(), Opts),
    #{ <<"image">> := WASMImageID } = dev_wasm:cache_wasm_image(WASMImage, Opts),
    hb_message:commit(
        hb_maps:merge(
            hb_message:uncommitted(base_process(Opts), Opts),
            #{
                <<"execution-device">> => <<"stack@1.0">>,
                <<"device-stack">> => [<<"wasm-64@1.0">>],
                <<"image">> => WASMImageID
            },
			Opts
        ),
        Opts#{ <<"priv-wallet">> => Wallet }
    ).

%% @doc Generate a process message with a random number, and the 
%% `dev_wasm' device for execution.
aos_process() ->
    aos_process(#{}).
aos_process(Opts) ->
    aos_process(Opts, [
        <<"wasi@1.0">>,
        <<"json-iface@1.0">>,
        <<"wasm-64@1.0">>,
        <<"multipass@1.0">>
    ]).
aos_process(Opts, Stack) ->
    Wallet = hb_opts:get(priv_wallet, hb:wallet(), Opts),
    Address = hb_util:human_id(ar_wallet:to_address(Wallet)),
    WASMProc = wasm_process(<<"test/aos-2-pure-xs.wasm">>, Opts),
    hb_message:commit(
        hb_maps:merge(
            hb_message:uncommitted(WASMProc, Opts),
            #{
                <<"device-stack">> => Stack,
                <<"execution-device">> => <<"stack@1.0">>,
                <<"scheduler-device">> => <<"scheduler@1.0">>,
                <<"output-prefix">> => <<"wasm">>,
                <<"patch-from">> => <<"/results/outbox">>,
                <<"passes">> => 2,
                <<"stack-keys">> =>
                    [
                        <<"init">>,
                        <<"compute">>,
                        <<"snapshot">>,
                        <<"normalize">>
                    ],
                <<"scheduler">> =>
                    hb_opts:get(scheduler, Address, Opts),
                <<"authority">> =>
                    hb_opts:get(authority, Address, Opts)
            }, Opts),
        Opts#{ <<"priv-wallet">> => Wallet }
    ).

%% @doc Generate a device that has a stack of two `dev_test's for 
%% execution. This should generate a message state has doubled 
%% `Already-Seen' elements for each assigned slot.
test_process() ->
    test_process(#{}).
test_process(Opts) ->
    Wallet = hb:wallet(),
    hb_message:commit(
        hb_maps:merge(
            base_process(Opts),
            #{
                <<"execution-device">> => <<"stack@1.0">>,
                <<"device-stack">> => [<<"test-device@1.0">>, <<"test-device@1.0">>]
            }, 
            Opts
        ),
        Opts#{ <<"priv-wallet">> => Wallet }
    ).

schedule_test_message(Base, Text, Opts) ->
    schedule_test_message(Base, Text, #{}, Opts).
schedule_test_message(Base, Text, MsgBase, Opts) ->
    ?event(debug_test, {opts, Opts}),
    Wallet = hb_opts:get(priv_wallet, hb:wallet(), Opts),
    UncommittedBase = hb_message:uncommitted(MsgBase, Opts#{ <<"priv-wallet">> => Wallet }),
    Req =
        hb_message:commit(
            #{
                <<"path">> => <<"schedule">>,
                <<"method">> => <<"POST">>,
                <<"body">> =>
                    hb_message:commit(
                        UncommittedBase#{
                            <<"type">> => <<"Message">>,
                            <<"test-label">> => Text
                        },
                        Opts#{ <<"priv-wallet">> => Wallet }
                    )
            },
			Opts#{ <<"priv-wallet">> => Wallet }
        ),
    {ok, _} = hb_ao:resolve(Base, Req, Opts#{ <<"priv-wallet">> => Wallet }).

schedule_aos_call(Base, Code) ->
    schedule_aos_call(Base, Code, #{}).
schedule_aos_call(Base, Code, Opts) ->
    Wallet = hb_opts:get(priv_wallet, hb:wallet(), Opts),
    ProcID = hb_message:id(Base, all),
    Req =
        hb_message:commit(
            #{
                <<"action">> => <<"Eval">>,
                <<"data">> => Code,
                <<"target">> => ProcID
            },
            Opts#{ <<"priv-wallet">> => Wallet }
        ),
    schedule_test_message(Base, <<"TEST MSG">>, Req, Opts).

schedule_wasm_call(Base, FuncName, Params, Opts) ->
    Wallet = hb:wallet(),
    Req = 
        hb_message:commit(
            #{
                <<"path">> => <<"schedule">>,
                <<"method">> => <<"POST">>,
                <<"body">> =>
                    hb_message:commit(
                        #{
                            <<"type">> => <<"Message">>,
                            <<"function">> => FuncName,
                            <<"parameters">> => Params
                        },
                        Opts#{ <<"priv-wallet">> => Wallet }
                    )
            },
            Opts#{ <<"priv-wallet">> => Wallet }
        ),
    ?assertMatch({ok, _}, hb_ao:resolve(Base, Req, Opts)).

schedule_on_process_test_parallel_() ->
	{timeout, 30, fun()->
		Opts = test_opts(),
		Base = aos_process(Opts),
		schedule_test_message(Base, <<"TEST TEXT 1">>, Opts),
		schedule_test_message(Base, <<"TEST TEXT 2">>, Opts),
		?event(messages_scheduled),
		{ok, SchedulerRes} =
			hb_ao:resolve(Base, #{
				<<"method">> => <<"GET">>,
				<<"path">> => <<"schedule">>
			}, Opts),
		?assertMatch(
			<<"TEST TEXT 1">>,
			hb_ao:get(<<"assignments/0/body/test-label">>, SchedulerRes)
		),
		?assertMatch(
			<<"TEST TEXT 2">>,
			hb_ao:get(<<"assignments/1/body/test-label">>, SchedulerRes)
		)
	end}.

get_scheduler_slot_test_parallel() ->
    Opts = test_opts(),
    Base = base_process(Opts),
    schedule_test_message(Base, <<"TEST TEXT 1">>, Opts),
    schedule_test_message(Base, <<"TEST TEXT 2">>, Opts),
    Req = #{
        <<"path">> => <<"slot">>,
        <<"method">> => <<"GET">>
    },
    ?assertMatch(
        {ok, #{ <<"current">> := CurrentSlot }} when CurrentSlot > 0,
        hb_ao:resolve(Base, Req, Opts)
    ).

recursive_path_resolution_test_parallel() ->
    Opts = test_opts(),
    Base = base_process(Opts),
    schedule_test_message(Base, <<"TEST TEXT 1">>, Opts),
    CurrentSlot =
        hb_ao:resolve(
            Base,
            #{ <<"path">> => <<"slot/current">> },
            Opts#{ <<"hashpath">> => ignore }
        ),
    ?event({resolved_current_slot, CurrentSlot}),
    ?assertMatch(
        CurrentSlot when CurrentSlot > 0,
        CurrentSlot
    ),
    ok.

test_device_compute_test_parallel() ->
    Opts = test_opts(),
    Base = test_process(Opts),
    schedule_test_message(Base, <<"TEST TEXT 1">>, Opts),
    schedule_test_message(Base, <<"TEST TEXT 2">>, Opts),
    ?assertMatch(
        {ok, <<"TEST TEXT 2">>},
        hb_ao:resolve(
            Base,
            <<"schedule/assignments/1/body/test-label">>,
            Opts
        )
    ),
    Req = #{ <<"path">> => <<"compute">>, <<"slot">> => 1 },
    {ok, Res} = hb_ao:resolve(Base, Req, Opts),
    ?event({computed_message, {res, Res}}),
    ?assertEqual(1, hb_ao:get(<<"results/assignment-slot">>, Res, Opts)),
    ?assertEqual([1,1,0,0], hb_ao:get(<<"already-seen">>, Res, Opts)).

wasm_compute_test_parallel() ->
    Opts = test_opts(),
    Base = wasm_process(<<"test/test-64.wasm">>, Opts),
    schedule_wasm_call(Base, <<"fac">>, [2.0], Opts),
    schedule_wasm_call(Base, <<"fac">>, [3.0], Opts),
    schedule_wasm_call(Base, <<"fac">>, [4.0], Opts),
    schedule_wasm_call(Base, <<"fac">>, [5.0], Opts),
    schedule_wasm_call(Base, <<"fac">>, [6.0], Opts),
    {ok, _} = 
        hb_ao:resolve(
            Base,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 3 },
            Opts
        ),
    {ok, _} = 
        hb_ao:resolve(
            Base,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 3 },
            Opts
        ),
    {ok, _} = 
       hb_ao:resolve(
            Base,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 1 },
            Opts
        ),
    {ok, _} = 
        hb_ao:resolve(
            Base,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 2 },
            Opts
        ),
    {ok, _} = 
        hb_ao:resolve(
            Base,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 4 },
            Opts
        ),
    ok.
    % ?assertEqual([24.0], hb_ao:get(<<"results/output">>, Slot2Res, Opts)),
    % ?assertEqual([2.0], hb_ao:get(<<"results/output">>, Slot0Res, Opts)),
    % ?assertEqual([6.0], hb_ao:get(<<"results/output">>, Slot1Res, Opts)).

wasm_compute_from_id_test_parallel() ->
    Opts = test_opts(#{ <<"cache-control">> => <<"always">> }),
    Base = wasm_process(<<"test/test-64.wasm">>, Opts),
    schedule_wasm_call(Base, <<"fac">>, [5.0], Opts),
    BaseID = hb_message:id(Base, all, Opts),
    Req = #{ <<"path">> => <<"compute">>, <<"slot">> => 0 },
    {ok, Res} = hb_ao:resolve(BaseID, Req, Opts),
    ?event(process_compute, {computed_message, {res, Res}}),
    ?assertEqual([120.0], hb_ao:get(<<"results/output">>, Res, Opts)).

http_wasm_process_by_id_test_parallel() ->
    rand:seed(default),
    SchedWallet = ar_wallet:new(),
    Node = hb_http_server:start_node(Opts = #{
        <<"port">> => 10000 + rand:uniform(10000),
        <<"priv-wallet">> => SchedWallet,
        <<"cache-control">> => <<"always">>,
        <<"store">> => #{
            <<"store-module">> => hb_store_fs,
            <<"name">> => <<"cache-mainnet">>
        }
    }),
    Wallet = ar_wallet:new(),
    Proc = wasm_process(<<"test/test-64.wasm">>, Opts),
    hb_cache:write(Proc, Opts),
    ProcID = hb_util:human_id(hb_message:id(Proc, all, Opts)),
    InitRes =
        hb_http:post(
            Node,
            << "/schedule" >>,
            Proc,
            Opts
        ),
    ?event({schedule_proc_res, InitRes}),
    ExecMsg =
        hb_message:commit(
            #{
                <<"target">> => ProcID,
                <<"type">> => <<"Message">>,
                <<"function">> => <<"fac">>,
                <<"parameters">> => [5.0]
            },
            Opts#{ <<"priv-wallet">> => Wallet }
        ),
    {ok, Res} = hb_http:post(Node, << ProcID/binary, "/schedule">>, ExecMsg, Opts),
    ?event({schedule_msg_res, {res, Res}}),
    {ok, Msg4} =
        hb_http:get(
            Node,
            #{
                <<"path">> => << ProcID/binary, "/compute">>,
                <<"slot">> => 1
            },
            Opts
        ),
    ?event({compute_msg_res, {msg4, Msg4}}),
    ?assertEqual([120.0], hb_ao:get(<<"results/output">>, Msg4, Opts)).

aos_compute_test_parallel_() ->
    {timeout, 30, fun() ->
        Opts = test_opts(),
        Base = aos_process(Opts),
        schedule_aos_call(Base, <<"return 1+1">>, Opts),
        schedule_aos_call(Base, <<"return 2+2">>, Opts),
        Req = #{ <<"path">> => <<"compute">>, <<"slot">> => 0 },
        {ok, Res1} = hb_ao:resolve(Base, Req, Opts),
        {ok, Res2} = hb_ao:resolve(Res1, <<"results">>, Opts),
        ?event({computed_message, {res2, Res2}}),
        {ok, Data} = hb_ao:resolve(Res2, <<"data">>, Opts),
        ?event({computed_data, Data}),
        ?assertEqual(<<"2">>, Data),
        Msg4 = #{ <<"path">> => <<"compute">>, <<"slot">> => 1 },
        {ok, Res3} = hb_ao:resolve(Base, Msg4, Opts),
        ?assertEqual(<<"4">>, hb_ao:get(<<"results/data">>, Res3, Opts)),
        {ok, Res3}
    end}.

aos_browsable_state_test_parallel_() ->
    {timeout, 30, fun() ->
        Opts = test_opts(#{ <<"cache-control">> => <<"always">> }),
        Base = aos_process(Opts),
        schedule_aos_call(
            Base,
            <<"table.insert(ao.outbox.Messages, { target = ao.id, ",
                "action = \"State\", ",
                "data = { deep = 4, bool = true } })">>,
            Opts
        ),
        Req = #{ <<"path">> => <<"compute">>, <<"slot">> => 0 },
        {ok, Res} =
            hb_ao:resolve_many(
                [Base, Req, <<"results">>, <<"outbox">>, 1, <<"data">>, <<"deep">>],
                Opts
            ),
        ID = hb_message:id(Base, Opts),
        ?event({computed_message, {id, {explicit, ID}}}),
        ?assertEqual(4, Res)
    end}.

aos_state_access_via_http_test_parallel_() ->
    {timeout, 60, fun() ->
        rand:seed(default),
        Wallet = ar_wallet:new(),
        Node = hb_http_server:start_node(Opts = test_opts(#{
            <<"port">> => 10000 + rand:uniform(10000),
            <<"priv-wallet">> => Wallet,
            <<"cache-control">> => <<"always">>,
            <<"store">> => hb_test_utils:test_store(),
            <<"force-signed-requests">> => true
        })),
        Proc = aos_process(Opts),
        ProcID = hb_util:human_id(hb_message:id(Proc, all, Opts)),
        {ok, _InitRes} = hb_http:post(Node, <<"/schedule">>, Proc, Opts),
        Req = 
            hb_message:commit(
                #{
                    <<"data-protocol">> => <<"ao">>,
                    <<"variant">> => <<"ao.N.1">>,
                    <<"type">> => <<"Message">>,
                    <<"action">> => <<"Eval">>,
                    <<"data">> =>
                        <<"table.insert(ao.outbox.Messages, { target = ao.id,",
                            " action = \"State\", data = { ",
                                "[\"content-type\"] = \"text/html\", ",
                                "[\"body\"] = \"<h1>Hello, world!</h1>\"",
                            "}})">>,
                    <<"target">> => ProcID
                },
                Opts
            ),
        {ok, Res} = hb_http:post(Node, << ProcID/binary, "/schedule">>, Req, Opts),
        ?event({schedule_msg_res, {res, Res}}),
        {ok, Msg4} =
            hb_http:get(
                Node,
                #{
                    <<"path">> => << ProcID/binary, "/compute/results/outbox/1/data" >>,
                    <<"slot">> => 1
                },
                Opts
            ),
        ?event({compute_msg_res, {msg4, Msg4}}),
        ?event(
            {try_yourself,
                {explicit,
                    <<
                        Node/binary,
                        "/",
                        ProcID/binary,
                        "/compute&slot=1/results/outbox/1/data"
                    >>
                }
            }
        ),
        ?assertMatch(#{ <<"body">> := <<"<h1>Hello, world!</h1>">> }, Msg4),
        ok
    end}.

aos_state_patch_test_parallel_() ->
    {timeout, 30, fun() ->
        Wallet = hb:wallet(),
        Opts = test_opts(),
        BaseRaw = aos_process(Opts, [
            <<"wasi@1.0">>,
            <<"json-iface@1.0">>,
            <<"wasm-64@1.0">>,
            <<"patch@1.0">>,
            <<"multipass@1.0">>
        ]),
        {ok, Base} = hb_message:with_only_committed(BaseRaw, Opts),
        ProcID = hb_message:id(Base, all, Opts),
        InnerReq = 
            hb_message:commit(
                #{
                    <<"data-protocol">> => <<"ao">>,
                    <<"variant">> => <<"ao.N.1">>,
                    <<"target">> => ProcID,
                    <<"type">> => <<"Message">>,
                    <<"action">> => <<"Eval">>,
                    <<"data">> =>
                        <<
                            "table.insert(ao.outbox.Messages, "
                                "{ method = \"PATCH\", x = \"banana\" })"
                        >>
                },
                Opts#{ <<"priv-wallet">> => Wallet }
            ),
        Req = InnerReq#{
            <<"path">> => <<"schedule">>,
            <<"method">> => <<"POST">> 
        },
        {ok, _} = hb_ao:resolve(Base, Req, Opts),
        Res = #{ <<"path">> => <<"compute">>, <<"slot">> => 0 },
        {ok, Msg4} = hb_ao:resolve(Base, Res, Opts),
        ?event({computed_message, {res, Msg4}}),
        {ok, Data} = hb_ao:resolve(Msg4, <<"x">>, Opts),
        ?event({computed_data, Data}),
        ?assertEqual(<<"banana">>, Data)
    end}.

%% @doc Manually test state restoration without using the cache.
restore_test_parallel_() -> {timeout, 30, fun do_test_restore/0}.

do_test_restore() ->
    % Init the process and schedule 3 messages:
    % 1. Set variables in Lua.
    % 2. Return the variable.
    % Execute the first computation, then the second as a disconnected process.
    Opts = test_opts(#{
        <<"process-cache-frequency">> => 1
    }),
    Base = aos_process(Opts),
    schedule_aos_call(Base, <<"X = 42">>, Opts),
    schedule_aos_call(Base, <<"X = 1337">>, Opts),
    schedule_aos_call(Base, <<"return X">>, Opts),
    % Compute the first message.
    {ok, _} =
        hb_ao:resolve(
            Base,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 1 },
            Opts
        ),
    {ok, ResultB} =
        hb_ao:resolve(
            Base,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 2 },
            Opts
        ),
    ?event({result_b, ResultB}),
    ?assertEqual(<<"1337">>, hb_ao:get(<<"results/data">>, ResultB, Opts)).

now_results_test_parallel_() ->
    {timeout, 30, fun() ->
        Opts = test_opts(),
        Base = aos_process(Opts),
        schedule_aos_call(Base, <<"return 1+1">>, Opts),
        schedule_aos_call(Base, <<"return 2+2">>, Opts),
        ?assertEqual({ok, <<"4">>}, hb_ao:resolve(Base, <<"now/results/data">>, Opts))
    end}.

prior_results_accessible_test_parallel_() ->
    {timeout, 30, fun() ->
        Opts = test_opts(),
        Base = aos_process(Opts),
        schedule_aos_call(Base, <<"return 1+1">>, Opts),
        schedule_aos_call(Base, <<"return 2+2">>, Opts),
        ?assertEqual(
            {ok, <<"4">>},
            hb_ao:resolve(Base, <<"now/results/data">>, Opts)
        ),
        {ok, Results} = 
            hb_ao:resolve(
                Base,
                #{ <<"path">> => <<"compute">>, <<"slot">> => 1 },
                Opts
            ),
        ?assertMatch(
            #{ <<"results">> := #{ <<"data">> := <<"4">> } },
            hb_cache:ensure_all_loaded(Results, Opts)
        )
    end}.

persistent_process_test_parallel() ->
    {timeout, 30, fun() ->
        Opts = test_opts(),
        Base = aos_process(Opts),
        schedule_aos_call(Base, <<"X=1">>, Opts),
        schedule_aos_call(Base, <<"return 2">>, Opts),
        schedule_aos_call(Base, <<"return X">>, Opts),
        T0 = hb:now(),
        FirstSlotReq = #{
            <<"path">> => <<"compute">>,
            <<"slot">> => 0
        },
        ?assertMatch(
            {ok, _},
            hb_ao:resolve(Base, FirstSlotReq, Opts#{ <<"spawn-worker">> => true })
        ),
        T1 = hb:now(),
        ThirdSlotReq = #{
            <<"path">> => <<"compute">>,
            <<"slot">> => 2
        },
        Res = hb_ao:resolve(Base, ThirdSlotReq, Opts),
        ?event({computed_message, {res, Res}}),
        ?assertMatch(
            {ok, _},
            Res
        ),
        T2 = hb:now(),
        ?event(benchmark, {runtimes, {first_run, T1 - T0}, {second_run, T2 - T1}}),
        % The second resolve should be much faster than the first resolve, as the
        % process is already running.
        ?assert(T2 - T1 < ((T1 - T0)/2))
    end}.

simple_wasm_persistent_worker_benchmark_test_parallel() ->
    Opts = test_opts(),
    BenchTime = 0.05,
    Base = wasm_process(<<"test/test-64.wasm">>, Opts),
    schedule_wasm_call(Base, <<"fac">>, [5.0], Opts),
    schedule_wasm_call(Base, <<"fac">>, [6.0], Opts),
    {ok, Initialized} = 
        hb_ao:resolve(
            Base,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 1 },
            Opts#{ <<"spawn-worker">> => true, <<"process-workers">> => true }
        ),
    Iterations = hb_test_utils:benchmark(
        fun(Iteration) ->
            schedule_wasm_call(
                Initialized,
                <<"fac">>,
                [5.0],
                Opts
            ),
            ?assertMatch(
                {ok, _},
                hb_ao:resolve(
                    Initialized,
                    #{ <<"path">> => <<"compute">>, <<"slot">> => Iteration + 1 },
                    Opts
                )
            )
        end,
        BenchTime
    ),
    ?event(benchmark, {scheduled, Iterations}),
    hb_format:eunit_print(
        "Scheduled and evaluated ~p simple wasm process messages in ~p s (~s msg/s)",
        [Iterations, BenchTime, hb_util:human_int(Iterations / BenchTime)]
    ),
    ?assert(Iterations >= 1),
    ok.

aos_persistent_worker_benchmark_test_parallel_() ->
    {timeout, 30, fun() ->
        BenchTime = 0.25,
        init(),
        Base = aos_process(),
        schedule_aos_call(Base, <<"X=1337">>),
        FirstSlotReq = #{
            <<"path">> => <<"compute">>,
            <<"slot">> => 0
        },
        ?assertMatch(
            {ok, _},
            hb_ao:resolve(Base, FirstSlotReq, #{ <<"spawn-worker">> => true })
        ),
        Iterations = hb_test_utils:benchmark(
            fun(Iteration) ->
                schedule_aos_call(
                    Base,
                    <<"return X + ", (integer_to_binary(Iteration))/binary>>
                ),
                ?assertMatch(
                    {ok, _},
                    hb_ao:resolve(
                        Base,
                        #{ <<"path">> => <<"compute">>, <<"slot">> => Iteration },
                        #{}
                    )
                )
            end,
            BenchTime
        ),
        ?event(benchmark, {scheduled, Iterations}),
        hb_format:eunit_print(
            "Scheduled and evaluated ~p AOS process messages in ~p s (~s msg/s)",
            [Iterations, BenchTime, hb_util:human_int(Iterations / BenchTime)]
        ),
        ?assert(Iterations >= 1),
        ok
    end}.
